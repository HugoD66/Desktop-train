plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.25.0'
}

group 'com.example'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.9.2'
}

sourceCompatibility = '15'
targetCompatibility = '15'


tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainClass = 'com.example.gestionbudget.TableApplication'
}

javafx {
    version = '21-ea+24'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {
    // Database
    implementation 'org.xerial:sqlite-jdbc:3.44.1.0'
    implementation 'org.slf4j:slf4j-simple:1.7.32'

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
    useJUnitPlatform()
}

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'app'
    }
    jpackage {
        if(org.gradle.internal.os.OperatingSystem.current().windows){
            imageOptions += ['--icon', 'src/main/resources/com/example/gestionbudget/assets/picture/budget-icon.png']
            installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut']
        } else if(org.gradle.internal.os.OperatingSystem.current().macOsX){
            imageOptions += ['--icon', 'src/main/resources/com/example/gestionbudget/assets/picture/budget-icon.icns']
            installerOptions += ['--app-category', 'public.app-category.finance']

        } else if(org.gradle.internal.os.OperatingSystem.current().linux){
            imageOptions += ['--icon', 'src/main/resources/com/example/gestionbudget/assets/picture/budget-icon.png']

        }
    }
}

jlinkZip {
    group = 'distribution'
}